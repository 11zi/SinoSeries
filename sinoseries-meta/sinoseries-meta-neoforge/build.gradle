plugins {
    id 'architectury-plugin' version "${architect_plugin_version}"
    id 'dev.architectury.loom' version "${architectury_loom_version}"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    runs {
        client {
            configName = 'NeoForge Client'
            runDir = 'run/client'
        }

        server {
            configName = 'NeoForge Server'
            runDir = 'run/server'
        }
    }

    mods {
        subMod.each { Project mod ->
            helpers.getSubNeoForgeMod(mod).each { Project neoForgeMod ->
                "${mod.modid}" {
                    sourceSet neoForgeMod.sourceSets.main
                }
            }
        }
    }
}

dependencies {
//    neoForge "net.neoforged:neoforge:${project.neoforge_version}"
    neoForge "net.neoforged:forge:${project.minecraft_version}-${project.neoforge_version}"

//    modImplementation "com.ptsmods:devlogin:${project.mod_dev_login_version}"

    subMod.each { Project mod ->
        helpers.getSubNeoForgeMod(mod).each { Project neoForgeMod ->
            compileOnly project(path: neoForgeMod.path, configuration: 'namedElements')

            if (neoForgeMod.modid != 'sinotest') {
                include project(path: neoForgeMod.path, configuration: 'namedElements')
            }
        }
    }
}

processResources {
    doFirst {
        metaHelpers.injectNeoForgeTransformers(project)
    }

    Map<String, ?> propertiesToExpend = helpers.getPropertiesToExpand(project)
    List<String> filesToMatch = helpers.getFilesToMatchExpand()

    filesMatching(filesToMatch, (copy) -> {
        expand(propertiesToExpend)
    })
}

configureLaunch {
    doLast {
        metaHelpers.injectNeoForgeTransformers(project)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            version = helpers.getMavenVersion(project)
            from components.java
        }
    }
}
