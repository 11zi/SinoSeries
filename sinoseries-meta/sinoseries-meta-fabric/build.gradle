plugins {
    id 'architectury-plugin' version "${architect_plugin_version}"
    id 'dev.architectury.loom' version "${architectury_loom_version}"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    runs {
        client {
            configName = 'Fabric Client'
            runDir = 'run/client'
        }

        server {
            configName = 'Fabric Server'
            runDir = 'run/server'
        }
    }

    mods {
        subMod.each { Project mod ->
            helpers.getSubFabricMod(mod).each { Project fabricMod ->
                "${mod.modid}" {
                    sourceSet fabricMod.sourceSets.main
                }
            }
        }
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    modImplementation "maven.modrinth:modmenu:${project.mod_menu_version}"

//    modImplementation "com.ptsmods:devlogin:${project.mod_dev_login_version}"

    subMod.each { Project mod ->
        helpers.getSubFabricMod(mod).each { Project fabricMod ->
            implementation project(path: fabricMod.path, configuration: 'namedElements')

            if (fabricMod.modid != 'sinotest') {
                include project(path: fabricMod.path, configuration: 'namedElements')
            }
        }
    }
}

processResources {
    doFirst {
        metaHelpers.injectFabricTransformers(project)
    }

    Map<String, ?> propertiesToExpend = helpers.getPropertiesToExpand(project, true)
    List<String> filesToMatch = helpers.getFilesToMatchExpand()

    filesMatching(filesToMatch, (copy) -> {
        expand(propertiesToExpend)
    })
}

configureLaunch {
    doLast {
        metaHelpers.injectFabricTransformers(project)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            version = helpers.getMavenVersion(project)
            from components.java
        }
    }
}
