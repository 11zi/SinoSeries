plugins {
    id 'architectury-plugin' version "${architect_plugin_version}"
    id 'dev.architectury.loom' version "${architectury_loom_version}"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {
        // Workaround: qyl27: for forge subMod mixin.
        mixinConfig 'sinocore-forge.mixins.json'

        convertAccessWideners = true
    }

    runs {
        client {
            configName = 'Forge Client'
            runDir = 'run/client'
        }

        server {
            configName = 'Forge Server'
            runDir = 'run/server'
        }

        subMod.each { Project mod ->
            "data_${mod.modid}" {
                data()

                ideConfigGenerated = true
                configName = "Data Generator ${mod.modid}"

                programArgs '--all', '--mod', "${mod.modid}",
                        '--output', helpers.getProjectGeneratedResourcesDir(mod).absolutePath

                subMod.each { sub ->
                    helpers.getProjectResourcesDirs(sub).each { dir ->
                        programArgs '--existing', dir.absolutePath
                    }
                }
            }
        }
    }

    mods {
        subMod.each { Project mod ->
            helpers.getSubForgeMod(mod).each { Project forgeMod ->
                "${mod.modid}" {
                    sourceSet forgeMod.sourceSets.main
                }
            }
        }
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }

    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

    modRuntimeOnly group: 'maven.modrinth', name: 'jei', version: project.mod_jei_forge_version
    subMod.each { Project mod ->
        common(project(path: mod.path, configuration: 'namedElements')) { transitive true }

        helpers.getSubForgeMod(mod).each { Project forgeMod ->
            implementation project(path: forgeMod.path, configuration: 'namedElements')

            if (mod.modid != 'sinotest') {
                include project(forgeMod.path)
            }
        }
    }
}

processResources {
    Map<String, ?> propertiesToExpend = helpers.getPropertiesToExpand(project)
    List<String> filesToMatch = helpers.getFilesToMatchExpand()

    filesMatching(filesToMatch, (copy) -> {
        expand(propertiesToExpend)
    })
}

configureLaunch {
    doLast {
        subMod.each { Project mod ->
            File dir = helpers.getProjectGeneratedResourcesDir(mod)
            if (!dir.exists()) {
                dir.mkdirs()
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            version = helpers.getMavenVersion(project)
            from components.java
        }
    }
}
