import com.google.common.io.FileWriteMode
import com.google.common.io.Files

import java.nio.charset.StandardCharsets

plugins {
    id 'architectury-plugin' version "${architect_plugin_version}"
    id 'dev.architectury.loom' version "${architectury_loom_version}"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {

    }

    runs {
        client {
            configName = 'Forge Client'
            runDir = 'run/client'
        }

        server {
            configName = 'Forge Server'
            runDir = 'run/server'
        }
    }

    mods {
        subMod.each { Project mod ->
            helpers.getSubForgeMod(mod).each { Project forgeMod ->
                "${mod.modid}" {
                    sourceSet forgeMod.sourceSets.main
                }
            }
        }
    }
}

dependencies {
    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

    subMod.each { Project mod ->
        helpers.getSubForgeMod(mod).each { Project forgeMod ->
            include project(path: forgeMod.path, configuration: 'namedElements')
        }
    }
}

classes {
    subMod.each { Project mod ->
        helpers.getSubForgeMod(mod).each { Project forgeMod ->
            dependsOn("${mod.path}:classes")
            dependsOn("${forgeMod.path}:classes")
        }
    }
}

processResources {
    var projectTransformsFile = new File(projectDir, '/.gradle/architectury/.transforms')
    java.nio.file.Files.deleteIfExists(projectTransformsFile.toPath())
    java.nio.file.Files.createFile(projectTransformsFile.toPath())

    subMod.each { Project mod ->
        helpers.getSubForgeMod(mod).each { Project forgeMod ->
            var subModTransformsFile = new File(forgeMod.projectDir, '/.gradle/architectury/.transforms')
            var lines = java.nio.file.Files.readString(subModTransformsFile.toPath())
            Files.asCharSink(projectTransformsFile, StandardCharsets.UTF_8, FileWriteMode.APPEND)
                    .write(lines)
        }
    }

    println 'Architectury transformers injected.'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            version = helpers.getMavenVersion(project)
            from components.java
        }
    }
}
